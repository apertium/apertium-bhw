# Morphological Transducer for Biak

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <adv>    # Adverb
# <prn>    # pronoun
# <det>    # determiner
# <cnjcoo> # coordinating conjunction
# <cnjsub> # subordinating conjunction
# <pr>     # preposition

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Verbs
# <tv>   # transitive
# <iv>   # intransitive
# <p1>   # 1st person
# <p2>   # 2nd person
# <p3>   # 3rd person
# <sg>   # singular
# <du>   # dual
# <pc>   # paucal
# <pl>   # plural
# <inc>  # inclusive
# <ex>   # exclusive
# <an>   # animate
# <inan> # inanimate

# Complex articles
# <pers> # personal
# <spc>  # specific
# <giv>  # givenness
# <dem>  # demonstrative

# Possessive

# <pos> # possessive 
# <px1sg> # possessor, 1st person
# <px2sg> # possessor, 2nd person
# <px3sg> # possessor, 3rd person
# <px1du> # possessor, 1st person dual
# <pxinc> # possessor, inclusive
# <pxex> # possessor, exclusive
# <px2du> # possessor, 2nd person dual
# <px3du> # possessor, 3rd person dual
# <px1pc> # possessor, paucal
# <px1pl> # possessor, 1st person plural
# <px2pl> # possessor, 2nd person plural
# <px3pl> # possessor, 3rd person plural
# <an> # animate
# <inan> # inanimate




# Other symbols
# >      # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS
Punctuation
VerbPat
NounPat
ComplexArticle
Possessor Possessum [<spc>:]
Conjunction
Prepositions
:InalienablePrefix InalienableRoot [<n><pos>:] InalienablePrefix: InalienableSuffix
InalienableRoot [<n><pos>:] InalienableSuffix
Adverb

PATTERN VerbPat
:Verb-CC-Affix Verb-CC Verb-CC-Affix:
:Verb-V-Affix  Verb-V  Verb-V-Affix:
:Verb-CV-Affix Verb-CV Verb-CV-Affix:

PATTERN NounPat
Noun [<n>:] NounSpecificity?
Noun [<n>:] [+:>] Pronoun-Specifier

PATTERN ComplexArticle
Pronoun-Specifier DemonstrativeDeterminer? MotionPat?
:Givenness Pronoun-Specifier Givenness: DemonstrativeDeterminer? MotionPat?

PATTERN DemonstrativeDeterminer
[+:] Directional? Demonstrative [<det><dem>:]

PATTERN MotionPat
[+:] Motion

###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

LEXICON Directional
rum:>rum
ri:>ri
ra:>ra
re:>re
pon:>pon
pur:>pur
yas:>yas
vav:>vas
vun:>vun
var:>var

LEXICON Demonstrative
ne:>ne
ya:>ya
wa:>wa

LEXICON Motion
ma<adv>:>ma
fa<adv>:>fa
ra<adv>:>ra

LEXICON Givenness

<giv>:{AN}
<giv>:nan>

LEXICON Pronoun-Specifier

aya<prn><pers><p1><sg><spc>:yai
aw<prn><pers><p2><sg><spc>:aw{YA}
i<prn><pers><p3><sg><spc>:{YA}
ku<prn><pers><p1><du><inc><spc>:ku{YA}
nu<prn><pers><p1><du><ex><spc>:nu{YA}
mu<prn><pers><p2><du><spc>:mu{YA}
su<prn><pers><p3><du><spc>:su{YA}
sko<prn><pers><p3><pc><spc>:sko{YA}
ko<prn><pers><p1><pl><inc><spc>:ko{YA}
inko<prn><pers><p1><pl><ex><spc>:inko{YA}
mko<prn><pers><p2><pl><spc>:mko{YA}
si<prn><pers><p3><pl><an><spc>:s{YA}
na<prn><pers><p3><pl><inan><spc>:na

LEXICON Verb-CV-Affix

<p1><sg>:ya>
<p2><sg>:{2}
<p3><sg>:{3}
<p1><du><ex>:nu>
<p1><du><inc>:ku>
<p2><du>:mu>
<p3><du>:su>
<p3><pc>:sko>
<p1><pl><ex>:nko>
<p1><pl><inc>:ko>
<p2><pl>:mko>
<p3><pl><an>:si>
<p3><pl><inan>:na>

LEXICON Verb-CV

so<v><tv>:s{W}o # throw


LEXICON Verb-CC #need to add transivity?

srow<v><tv>:srow # meet
ba<v><iv>:ba # big

LEXICON Verb-CC-Affix

<p1><sg>:ya>
<p2><sg>:wa>
<p3><sg>:i>
<p1><du><ex>:nu>
<p1><du><inc>:ku>
<p2><du>:mu>
<p3><du>:su>
<p3><pc>:sko>
<p1><pl><ex>:nko>
<p1><pl><inc>:ko>
<p2><pl>:mko>
<p3><pl><an>:si>
<p3><pl><inan>:na>

LEXICON Verb-V

arok<v><iv>:arok # be straight
obe<v><tv>:obe # say

LEXICON Verb-V-Affix

<p1><sg>:y>
<p2><sg>:w>
<p3><sg>:d>
<p1><du><ex>:nuy>
<p1><du><inc>:kuy>
<p2><du>:muy>
<p3><du>:suy>
<p3><pc>:sk>
<p1><pl><ex>:nk>
<p1><pl><inc>:k>
<p2><pl>:mk>
<p3><pl><an>:s>
<p3><pl><inan>:n>

LEXICON Noun

yuk:yuk # ukelele
Wardo:Wardo #Wardo (a place)
snewar:snewar #belly
mnu:mnu #village

LEXICON NounSpecificity

<nspc><sg>:>o
<nspc><pl>:>no

LEXICON Possessor
det<det><pos><px1sg>:aye> # Dir/LR
det<det><pos><px1sg>:ye>
det<det><pos><px2sg>:be>
det<det><pos><px3sg>:vye>
det<det><pos><px3sg>:ye> # Dir/LR
det<det><pos><px1du><inc>:kuve>
det<det><pos><px1du><ex>:nuve>
det<det><pos><px2du>:muve>
det<det><pos><px3du>:suve>
det<det><pos><px1pc>:skove>
det<det><pos><px1pl><pxinc>:kove>
det<det><pos><px1pl><pxex>:nkove> # Dir/LR
det<det><pos><px1pl><pxex>:inkove>
det<det><pos><px2pl>:mkove>
det<det><pos><px3pl><an>:se>
det<det><pos><px3pl><inan>:nbe>

LEXICON Possessum
<sg>:d{YA}
<du>:su{YA}
<pc>:sko{YA}
<pl><an>:s{YA}
<pl><inan>:na

LEXICON Conjunction 

ma<cnjcoo>:ma #and
kukr<cnjsub>:kukr #with, because
kukru<cnjsub>:kukr #with, because
infa<cnjsub>:infa #so that
fa<cnjsub>:fa #so that
ra<cnjsub>:ra #until
indya<cnjcoo>:indya #so
inja<cnjcoo>:inja #so
vo<cnjsub>:vo #while
voi<cnjcoo>:voi #but
imboi<cnjcoo>:imboi #but
indo<cnjsub>:indo #then
ido<cnjsub>:ido #when, THEM

LEXICON Prepositions
ro<pr>:ro # at
ra<pr>:ra # along, over
ve<pr>:ve # to
kukr<pr>:kukr # with
snar<pr>:snar # because of
ker<pr>:ker # from
bur<pr>:bur # from
yov<pr>:yov # to

LEXICON InalienableRoot
we:we #leg
vru:vru #head
vra:vra #arm
sne:sne #belly area
fi:fi #vagina
si:si #penis
do:do #inside
sva:sva #mouth
na:na #tooth
snoni:snoni #nose
mka:mka #eye
sasu:sasu #neck

LEXICON InalienableSuffix
<px1sg><sg>:>ri
<px2sg><sg>:>mri
<px3sg><sg>:>ri
<px1sg><pl>:>si
<px2sg><pl>:>msi
<px3sg><pl>:>si
<du>:>{M}sna
<pc>:>{M}sna
<pl>:>{M}sna


LEXICON InalienablePrefix

<px1du><pxinc>:ku>
<px1du><pxex>:nu>
<px2du>:mu>
<px3du>:su>
<px3pc>:sko>
<px1pl><pxinc>:ko>
<px1pl><pxex>:nko>
<px2pl>:mko>
<px3pl>:si>

LEXICON Adverb

wer<adv>:wer #again
kwar<adv>:kwar #already
kaku<adv>:kaku #very






LEXICON Punctuation

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:? 
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# http://wiki.apertium.org/wiki/Hfst

